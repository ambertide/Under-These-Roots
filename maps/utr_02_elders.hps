void OnStart()
{
    AddEntityCollideCallback("Player", "PlayerPassOut_1", "PlayerPass", true, 0);
    AddEntityCollideCallback("Player", "PlayerPassOut_2", "PlayerPass", true, 0);
    AddEntityCollideCallback("Player", "MapChange", "PlayerPass", true, 0);
	AddEntityCollideCallback("Player", "PlayerMonsterPass", "PlayerPass", false, 0);
	AddUseItemCallback("key_open_prison", "key_tomb_rusty_1", "prison_1", "ItemUse", true);
    SetEntityCallbackFunc("lantern_1", "EntityCallback");
}

void EntityCallback(string &in asEntity, string&in type) {
    if (asEntity == "lantern_1") {
        if (type == "OnPickup") {
            SetLightVisible("lantern_light", false);
        }
    }
}

void ItemUse(string &in asItem, string &in asEntity) {
	if (asEntity == "prison_1") {
		SetSwingDoorLocked("prison_1", false, true);
	}
}

void PlayerPass(string &in asParent, string &in asChild, int alState) {
    if (asChild == "PlayerPassOut_1") {
        SetPlayerActive(false);
        GivePlayerDamage(25.0f, "BlodSplat", false, false);
        FadePlayerRollTo(40.0f, 15.0f, 30.0f);
        FadeOut(0.5f);
        PlaySoundAtEntity("", "player_falldamage_med.snt", "Player", 0, false);
        SetLocalVarInt("playerDragged", 1);
        AddTimer("FirstPass", 3.0f, "TimerDependant");
        StopMusic(0.01f, 0);
        PlayMusic("18_amb.ogg", true, 0.5f, 0.9f, 1, false);
    } else if (asChild == "PlayerPassOut_2") {
        SetLocalVarInt("playerDragged", 0);
        AddTimer("SecondPass", 0, "TimerDependant");
    } else if (asChild == "MapChange") {
        ChangeMap("utr_03_forest", "PlayerStartArea_1", "", "");
    } else if (asChild == "PlayerMonsterPass") {
		if (HasItem("key_tomb_rusty_1")) {
			RemoveEntityCollideCallback("Player", "PlayerPass");
			StopMusic(1.0f, 0);
			AddTimer("PrepMayhem", 1.0f, "TimerDependant");
		}	
	}
}

void TimerDependant(string &in asTimer) {
    if (asTimer == "FirstPass") {
        //Play lock opening and dragging sounds
        TeleportPlayer("PlayerStartArea_2");
        FadeIn(3.0f);
        AddTimer("Dragged", 3.0f, "TimerDependant");
    } else if (asTimer == "Dragged") {
        AddTimer("ItelePlayer", 0, "TimerDependant");
    } else if (asTimer == "ItelePlayer") {
        if (GetLocalVarInt("playerDragged") == 1) {
            AddTimer("ItelePlayer", 0, "TimerDependant");
            MovePlayerForward(-0.5);
        } else {
            SetPlayerActive(true);
            // Some random voices
        }
    } else if (asTimer == "SecondPass") {
        FadeOut(1.0f);
        AddTimer("TeleportPlayerDuringSecondPass", 1.0f, "TimerDependant");
    } else if (asTimer == "TeleportPlayerDuringSecondPass") {
        TeleportPlayer("PlayerStartArea_3");
        AddTimer("PlayerReturnNormal", 1.0f, "TimerDependant");
    } else if (asTimer == "PlayerReturnNormal") {
        FadeIn(1.0f);
        FadePlayerRollTo(0, 100.0f, 100.0f);
    } else if (asTimer == "PrepMayhem") {
		InitiateMayhem();
	}
}

void InitiateMayhem() {
//    PlayMusic("utr_02_elders_amb.ogg", true, 0.5f, 0.9f, 1, false);
	PlaySoundAtEntity("", "other_enabled.snt", "corpse_pile1_1", 0, false);
	PlaySoundAtEntity("", "29_scream.snt", "corpse_pile_1", 0, false);
    PlaySoundAtEntity("", "15_ripp_door.snt", "prison_section_silent_1", 0, false);
}
